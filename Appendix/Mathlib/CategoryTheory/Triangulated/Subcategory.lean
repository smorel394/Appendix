/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Localization.CalculusOfFractions
import Mathlib.CategoryTheory.Localization.Triangulated
import Mathlib.CategoryTheory.Limits.FullSubcategory
import Mathlib.CategoryTheory.Shift.Localization
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.CategoryTheory.Triangulated.Subcategory
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Appendix.Mathlib.CategoryTheory.Shift.CommShift
import Appendix.Mathlib.CategoryTheory.Triangulated.Functor
import Appendix.Mathlib.CategoryTheory.Triangulated.Pretriangulated
import Appendix.Mathlib.CategoryTheory.ObjectProperty.Shift
import Appendix.Mathlib.CategoryTheory.ObjectProperty.ContainsZero

/-! # Triangulated subcategories

Addendum to the mathlib file.

-/

assert_not_exists TwoSidedIdeal

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

namespace Limits

variable {C J‚ÇÅ J‚ÇÇ : Type _} [Category C]
  (X : J‚ÇÇ ‚Üí C) (e : J‚ÇÅ ‚âÉ J‚ÇÇ) [HasProduct X]

noncomputable def fanOfEquiv : Fan (X ‚àò e) := Fan.mk (‚àè·∂ú X) (fun _ => Pi.œÄ _ _)

@[simp]
lemma fanOfEquiv_proj (j : J‚ÇÅ) : (fanOfEquiv X e).proj j = Pi.œÄ _ (e j) := rfl

@[reassoc]
lemma Fan.congr_proj {J : Type _} {F : J ‚Üí C} (s : Fan F)
    {j‚ÇÅ j‚ÇÇ : J} (h : j‚ÇÅ = j‚ÇÇ) : s.proj j‚ÇÅ ‚â´ eqToHom (by rw [h]) = s.proj j‚ÇÇ := by
  subst h
  simp

@[reassoc]
lemma Pi.congr_œÄ {J : Type _} (F : J ‚Üí C) [HasProduct F] {j‚ÇÅ j‚ÇÇ : J} (h : j‚ÇÅ = j‚ÇÇ) :
    Pi.œÄ F j‚ÇÅ ‚â´ eqToHom (by rw [h]) = Pi.œÄ F j‚ÇÇ := by
  subst h
  simp

noncomputable def isLimitFanOfEquiv : IsLimit (fanOfEquiv X e) :=
  mkFanLimit _ (fun s => Pi.lift (fun j‚ÇÇ => s.proj (e.symm j‚ÇÇ) ‚â´ eqToHom (by simp) ))
    (fun s j => by simp [Fan.congr_proj _ (e.symm_apply_apply j)])
    (fun s m hm => Limits.Pi.hom_ext (f := X) _ _ (fun j ‚Ü¶ by simp [‚Üê hm]))

lemma hasProductOfEquiv : HasProduct (X ‚àò e) :=
  ‚ü®‚ü®_, isLimitFanOfEquiv X e‚ü©‚ü©

noncomputable def productIsoOfEquiv [HasProduct (X ‚àò e)] : ‚àè·∂ú (X ‚àò e) ‚âÖ ‚àè·∂ú X :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (isLimitFanOfEquiv X e)

noncomputable def productOptionIso {C J : Type _} [Category C]
    (X : Option J ‚Üí C) [HasProduct X] [HasProduct (fun j => X (some j))]
    [HasBinaryProduct (‚àè·∂ú (fun j => X (some j))) (X none)] :
    (‚àè·∂ú X) ‚âÖ (‚àè·∂ú (fun j => X (some j))) ‚®Ø (X none) where
  hom := prod.lift (Pi.lift (fun j => Pi.œÄ _ (some j))) (Pi.œÄ _ none)
  inv := Pi.lift (fun b => match b with
    | some j => prod.fst ‚â´ Pi.œÄ _ j
    | none => prod.snd)

end Limits

namespace Triangulated

open Pretriangulated ObjectProperty

variable (C : Type*) [Category C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Subcategory

variable {C}
variable (S : Subcategory C)

lemma mem_of_isZero [IsClosedUnderIsomorphisms S.P] (X : C) (hX : IsZero X) : S.P X :=
  prop_of_iso _ hX.isoZero.symm S.zero

instance : IsClosedUnderIsomorphisms S.isoClosure.P := by
  dsimp only [isoClosure]
  infer_instance

section

variable (P : C ‚Üí Prop) (zero : P 0)
  (shift : ‚àÄ (X : C) (n : ‚Ñ§), P X ‚Üí P (X‚ü¶n‚üß))
  (ext‚ÇÇ : ‚àÄ (T : Triangle C) (_ : T ‚àà distTriang C), P T.obj‚ÇÅ ‚Üí P T.obj‚ÇÉ ‚Üí P T.obj‚ÇÇ)

instance : IsClosedUnderIsomorphisms (mk' P zero shift ext‚ÇÇ).P where
  of_iso {X Y} e hX := by
    refine ext‚ÇÇ (Triangle.mk e.hom (0 : Y ‚ü∂ 0) 0) ?_ hX zero
    refine isomorphic_distinguished _ (contractible_distinguished X) _ ?_
    exact Triangle.isoMk _ _ (Iso.refl _) e.symm (Iso.refl _)

end

@[simp]
lemma shift_iff [IsClosedUnderIsomorphisms S.P] (X : C) (n : ‚Ñ§) :
    S.P (X‚ü¶n‚üß) ‚Üî S.P X := by
  constructor
  ¬∑ intro h
    exact prop_of_iso _ ((shiftEquiv C n).unitIso.symm.app X) (S.shift _ (-n) h)
  ¬∑ exact S.shift X n

/-- Variant of `mem_W_iff_of_distinguished`. -/
lemma mem_W_iff_of_distinguished'
    [IsClosedUnderIsomorphisms S.P] (T : Triangle C) (hT : T ‚àà distTriang C) :
    S.W T.mor‚ÇÇ ‚Üî S.P T.obj‚ÇÅ := by
  have := S.mem_W_iff_of_distinguished _ (rot_of_distTriang _ hT)
  dsimp at this
  rw [this, shift_iff]

section

variable (T : Triangle C) (hT : T ‚àà distTriang C)

include hT

omit hT in
lemma binary_product_stable [IsClosedUnderIsomorphisms S.P]
    (X‚ÇÅ X‚ÇÇ : C) (hX‚ÇÅ : S.P X‚ÇÅ) (hX‚ÇÇ : S.P X‚ÇÇ) :
    S.P (X‚ÇÅ ‚®Ø X‚ÇÇ)  :=
  S.ext‚ÇÇ _ (binaryProductTriangle_distinguished X‚ÇÅ X‚ÇÇ) hX‚ÇÅ hX‚ÇÇ

omit hT in
lemma pi_finite_stable [IsClosedUnderIsomorphisms S.P]
    {J : Type} [Finite J] (X : J ‚Üí C) (hX : ‚àÄ j, S.P (X j)) :
    S.P (‚àè·∂ú X) := by
  revert hX X
  let P : Type ‚Üí Prop := fun J =>
    ‚àÄ [hJ : Finite J] (X : J ‚Üí C), (‚àÄ (j : J), S.P (X j)) ‚Üí S.P (‚àè·∂ú X)
  apply @Finite.induction_empty_option (P := P)
  ¬∑ intro J‚ÇÅ J‚ÇÇ e hJ‚ÇÅ _ X hX
    have : Finite J‚ÇÅ := Finite.of_equiv _ e.symm
    exact prop_of_iso _ (productIsoOfEquiv X e) (hJ‚ÇÅ (fun j‚ÇÅ => X (e j‚ÇÅ)) (fun j‚ÇÅ => hX _))
  ¬∑ intro _ X _
    refine prop_of_iso _ (IsZero.isoZero ?_).symm S.zero
    rw [IsZero.iff_id_eq_zero]
    ext ‚ü®‚ü©
  ¬∑ intro J _ hJ _ X hX
    exact prop_of_iso _ (productOptionIso  X).symm
      (S.binary_product_stable _ _ (hJ (fun j => X (some j)) (fun j => hX _)) (hX none))

instance : S.W.IsStableUnderFiniteProducts := by
  rw [‚Üê isoClosure_W]
  exact ‚ü®fun J _ => by
    refine MorphismProperty.IsStableUnderProductsOfShape.mk _ _ ?_
    intro X‚ÇÅ X‚ÇÇ _ _ f hf
    exact W.mk _ (productTriangle_distinguished _
      (fun j => (hf j).choose_spec.choose_spec.choose_spec.choose))
      (pi_finite_stable _ _ (fun j => (hf j).choose_spec.choose_spec.choose_spec.choose_spec))‚ü©

section

variable (S' : Subcategory C) [IsClosedUnderIsomorphisms S.P]
    [IsClosedUnderIsomorphisms S'.P]

def inter : Subcategory C :=
  mk' (fun X => S.P X ‚àß S'.P X) ‚ü®S.zero, S'.zero‚ü©
    (fun X n hX => ‚ü®S.shift X n hX.1, S'.shift X n hX.2‚ü©)
    (fun T hT h‚ÇÅ h‚ÇÉ => ‚ü®S.ext‚ÇÇ T hT h‚ÇÅ.1 h‚ÇÉ.1, S'.ext‚ÇÇ T hT h‚ÇÅ.2 h‚ÇÉ.2‚ü©)

instance : IsClosedUnderIsomorphisms (S.inter S').P := by
  dsimp [inter]
  infer_instance

end

end

def category := S.P.FullSubcategory

instance : Category S.category := FullSubcategory.category _

def Œπ : S.category ‚•§ C := ObjectProperty.Œπ _

def fullyFaithfulŒπ : S.Œπ.FullyFaithful := ObjectProperty.fullyFaithfulŒπ _

instance : S.Œπ.Full := (fullyFaithfulŒπ _).full
instance : S.Œπ.Faithful := (fullyFaithfulŒπ _).faithful

instance : Preadditive S.category := by
  dsimp [category]
  infer_instance

instance : S.Œπ.Additive := by
  dsimp [Œπ, category]
  infer_instance

lemma Œπ_obj_mem (X : S.category) : S.P (S.Œπ.obj X) := X.2

noncomputable instance hasShift : HasShift S.category ‚Ñ§ :=
  S.fullyFaithfulŒπ.hasShift (fun n => ObjectProperty.lift _ (S.Œπ ‚ãô shiftFunctor C n)
    (fun X => S.shift _ _ X.2))
    (fun _ => ObjectProperty.liftCompŒπIso _ _ _)

instance commShiftŒπ : S.Œπ.CommShift ‚Ñ§ :=
  Functor.CommShift.of_hasShiftOfFullyFaithful _ _ _

-- these definitions are made irreducible to prevent (at least temporarily) any abuse of defeq
attribute [irreducible] hasShift commShiftŒπ

instance (n : ‚Ñ§) : (shiftFunctor S.category n).Additive := by
  have := Functor.additive_of_iso (S.Œπ.commShiftIso n).symm
  apply Functor.additive_of_comp_faithful _ S.Œπ

instance : HasZeroObject S.category where
  zero := by
    obtain ‚ü®Z, hZ, mem‚ü© := S.zero'
    refine ‚ü®‚ü®Z, mem‚ü©, ?_‚ü©
    rw [IsZero.iff_id_eq_zero]
    apply hZ.eq_of_src

noncomputable instance : Pretriangulated S.category where
  distinguishedTriangles := fun T => S.Œπ.mapTriangle.obj T ‚àà distTriang C
  isomorphic_distinguished := fun T‚ÇÅ hT‚ÇÅ T‚ÇÇ e =>
    isomorphic_distinguished _ hT‚ÇÅ _ (S.Œπ.mapTriangle.mapIso e)
  contractible_distinguished X := by
    refine isomorphic_distinguished _ (contractible_distinguished (S.Œπ.obj X)) _ ?_
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) S.Œπ.mapZeroObject
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  distinguished_cocone_triangle {X Y} f := by
    obtain ‚ü®Z', g', h', mem‚ü© := distinguished_cocone_triangle (S.Œπ.map f)
    obtain ‚ü®Z'', hZ'', ‚ü®e‚ü©‚ü© := S.ext‚ÇÉ' _ mem X.2 Y.2
    let Z : S.category := ‚ü®Z'', hZ''‚ü©
    refine ‚ü®Z, S.Œπ.preimage (g' ‚â´ e.hom),
      S.Œπ.preimage (e.inv ‚â´ h' ‚â´ (S.Œπ.commShiftIso (1 : ‚Ñ§)).inv.app X),
      isomorphic_distinguished _ mem _ ?_‚ü©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) e.symm
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  rotate_distinguished_triangle T :=
    (rotate_distinguished_triangle (S.Œπ.mapTriangle.obj T)).trans
      (distinguished_iff_of_iso (S.Œπ.mapTriangleRotateIso.app T))
  complete_distinguished_triangle_morphism T‚ÇÅ T‚ÇÇ hT‚ÇÅ hT‚ÇÇ a b comm := by
    obtain ‚ü®c, ‚ü®hc‚ÇÅ, hc‚ÇÇ‚ü©‚ü© := complete_distinguished_triangle_morphism (S.Œπ.mapTriangle.obj T‚ÇÅ)
      (S.Œπ.mapTriangle.obj T‚ÇÇ) hT‚ÇÅ hT‚ÇÇ (S.Œπ.map a) (S.Œπ.map b)
      (by simpa using S.Œπ.congr_map comm)
    have ‚ü®c', hc'‚ü© : ‚àÉ (c' : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ), c = S.Œπ.map c' := ‚ü®S.Œπ.preimage c, by simp‚ü©
    dsimp at hc‚ÇÅ hc‚ÇÇ
    rw [hc'] at hc‚ÇÅ
    rw [hc', assoc, ‚Üê Functor.commShiftIso_hom_naturality] at hc‚ÇÇ
    refine ‚ü®c', ‚ü®S.Œπ.map_injective ?_, S.Œπ.map_injective ?_‚ü©‚ü©
    ¬∑ simpa using hc‚ÇÅ
    ¬∑ rw [‚Üê cancel_mono ((Functor.commShiftIso (Œπ S) (1 : ‚Ñ§)).hom.app T‚ÇÇ.obj‚ÇÅ),
        S.Œπ.map_comp, S.Œπ.map_comp, assoc, assoc, hc‚ÇÇ]

instance : S.Œπ.IsTriangulated := ‚ü®fun _ hT => hT‚ü©

instance [IsTriangulated C] : IsTriangulated S.category :=
  IsTriangulated.of_fully_faithful_triangulated_functor S.Œπ


section

variable {D : Type*} [Category D] [HasZeroObject D] [Preadditive D]
    [HasShift D ‚Ñ§] [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
    (F : C ‚•§ D) [F.CommShift ‚Ñ§] [F.IsTriangulated] [F.Full]

-- note: does not require `[Faithful F]` !

def essImage : Subcategory D :=
  Subcategory.mk' F.essImage ‚ü®0, ‚ü®F.mapZeroObject‚ü©‚ü©
    (fun X n ‚ü®Y, ‚ü®e‚ü©‚ü© => ‚ü®(shiftFunctor C n).obj Y,
      ‚ü®(F.commShiftIso n).app Y ‚â™‚â´ (shiftFunctor D n).mapIso e‚ü©‚ü©)
    (fun T hT ‚ü®X‚ÇÅ, ‚ü®e‚ÇÅ‚ü©‚ü© ‚ü®X‚ÇÉ, ‚ü®e‚ÇÉ‚ü©‚ü© => by
      have ‚ü®h, hh‚ü© := F.map_surjective (e‚ÇÉ.hom ‚â´ T.mor‚ÇÉ ‚â´ e‚ÇÅ.inv‚ü¶1‚üß' ‚â´
        (F.commShiftIso (1 : ‚Ñ§)).inv.app X‚ÇÅ)
      obtain ‚ü®X‚ÇÇ, f, g, H‚ü© := distinguished_cocone_triangle‚ÇÇ h
      exact ‚ü®X‚ÇÇ, ‚ü®Triangle.œÄ‚ÇÇ.mapIso
        (isoTriangleOfIso‚ÇÅ‚ÇÉ _ _ (F.map_distinguished _ H) hT e‚ÇÅ e‚ÇÉ (by
          dsimp
          simp only [hh, assoc, Iso.inv_hom_id_app, Functor.comp_obj,
            comp_id, Iso.cancel_iso_hom_left, ‚Üê Functor.map_comp,
            Iso.inv_hom_id, Functor.map_id]))‚ü©‚ü©)

instance : IsClosedUnderIsomorphisms (essImage F).P  := by
  dsimp only [essImage]
  infer_instance

end

section

variable {D : Type*} [Category D] (F : D ‚•§ C) (hF : ‚àÄ (X : D), S.P (F.obj X))

def lift : D ‚•§ S.category := ObjectProperty.lift S.P F hF

def liftCompInclusion : S.lift F hF ‚ãô S.Œπ ‚âÖ F :=   liftCompŒπIso _ _ _

instance [F.Faithful] : (S.lift F hF).Faithful :=
  Functor.Faithful.of_comp_iso (S.liftCompInclusion F hF)

instance [F.Full] : (S.lift F hF).Full :=
  Functor.Full.of_comp_faithful_iso (S.liftCompInclusion F hF)

-- should be generalized
instance [Preadditive D] [F.Additive] : (S.lift F hF).Additive where
  map_add {X Y f g} := by
    apply S.Œπ.map_injective
    apply F.map_add

noncomputable instance [HasShift D ‚Ñ§] [F.CommShift ‚Ñ§] : (S.lift F hF).CommShift ‚Ñ§ :=
  Functor.CommShift.ofComp (S.liftCompInclusion F hF) ‚Ñ§

noncomputable instance [HasShift D ‚Ñ§] [F.CommShift ‚Ñ§] :
  NatTrans.CommShift (S.liftCompInclusion F hF).hom ‚Ñ§ :=
    Functor.CommShift.ofComp_compatibility _ _

instance isTriangulated_lift [HasShift D ‚Ñ§] [Preadditive D] [F.CommShift ‚Ñ§] [HasZeroObject D]
    [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D] [F.IsTriangulated]:
    (S.lift F hF).IsTriangulated := by
  rw [Functor.isTriangulated_iff_comp_right (S.liftCompInclusion F hF)]
  infer_instance

end

section

variable {D : Type*} [Category D] [Preadditive D] [HasZeroObject D] [HasShift D ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
  (F : D ‚•§ C) [F.CommShift ‚Ñ§] [F.IsTriangulated]
  [IsClosedUnderIsomorphisms S.P]

def inverseImage : Subcategory D :=
  Subcategory.mk' (fun X => S.P (F.obj X))
    (prop_of_iso _ F.mapZeroObject.symm S.zero)
    (fun X n hX => prop_of_iso _ ((F.commShiftIso n).symm.app X) (S.shift _ n hX))
    (fun _ hT h‚ÇÅ h‚ÇÉ => S.ext‚ÇÇ _ (F.map_distinguished _ hT) h‚ÇÅ h‚ÇÉ)

lemma mem_inverseImage_iff (X : D) :
    (S.inverseImage F).P X ‚Üî S.P (F.obj X) := by rfl

instance : IsClosedUnderIsomorphisms (S.inverseImage F).P where
  of_iso {X Y} e hX := by
    rw [mem_inverseImage_iff] at hX ‚ä¢
    exact prop_of_iso _ (F.mapIso e) hX

lemma mem_inverseImage_W_iff {X Y : D} (s : X ‚ü∂ Y) :
    (S.inverseImage F).W s ‚Üî S.W (F.map s) := by
  obtain ‚ü®Z, g, h, hT‚ü© := distinguished_cocone_triangle s
  have eq‚ÇÅ := (S.inverseImage F).mem_W_iff_of_distinguished _ hT
  have eq‚ÇÇ := S.mem_W_iff_of_distinguished _ (F.map_distinguished _ hT)
  dsimp at eq‚ÇÅ eq‚ÇÇ
  rw [eq‚ÇÅ, mem_inverseImage_iff, eq‚ÇÇ]

lemma inverseImage_W_isInverted {E : Type*} [Category E]
    (L : C ‚•§ E) [L.IsLocalization S.W] :
    (S.inverseImage F).W.IsInvertedBy (F ‚ãô L) :=
  fun X Y f hf => Localization.inverts L S.W (F.map f)
    (by simpa only [mem_inverseImage_W_iff] using hf)

end

section

variable {D : Type*} [Category D] [Preadditive D] [HasZeroObject D] [HasShift D ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
  {F G : C ‚•§ D} [F.CommShift ‚Ñ§] [G.CommShift ‚Ñ§] [F.IsTriangulated]
  [G.IsTriangulated] (œÑ : F ‚ü∂ G) [NatTrans.CommShift œÑ ‚Ñ§]

def ofNatTrans : Subcategory C :=
  Subcategory.mk' (fun X => IsIso (œÑ.app X))
    ‚ü®0, by rw [comp_zero, ‚Üê F.map_id, id_zero, F.map_zero],
        by rw [zero_comp, ‚Üê G.map_id, id_zero, G.map_zero]‚ü©
    (fun X n (_ : IsIso (œÑ.app X)) => by
      dsimp
      rw [NatTrans.app_shift œÑ n]
      infer_instance)
    (fun T hT h‚ÇÅ h‚ÇÉ => by
      exact Pretriangulated.isIso‚ÇÇ_of_isIso‚ÇÅ‚ÇÉ
        ((Pretriangulated.Triangle.homMk _ _ (œÑ.app _) (œÑ.app _) (œÑ.app _)
          (by simp) (by simp) (by simp [NatTrans.shift_app_comm œÑ])))
        (F.map_distinguished _ hT) (G.map_distinguished _ hT) (by exact h‚ÇÅ) (by exact h‚ÇÉ))

instance : IsClosedUnderIsomorphisms (ofNatTrans œÑ).P := by
  dsimp [ofNatTrans]
  infer_instance

end

section

variable {D : Type*} [Category D] [HasZeroObject D] [Preadditive D]
    [HasShift D ‚Ñ§] [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
    (F : C ‚•§ D) [F.CommShift ‚Ñ§] [F.IsTriangulated] [F.Full] [F.Faithful]

def map : Subcategory D := essImage (S.Œπ ‚ãô F)

instance : IsClosedUnderIsomorphisms (S.map F).P := by
  dsimp [map]
  infer_instance

lemma mem_map_iff (X : C) [IsClosedUnderIsomorphisms S.P] :
    (S.map F).P (F.obj X) ‚Üî S.P X := by
  constructor
  ¬∑ rintro ‚ü®‚ü®Y, hX‚ü©, ‚ü®e‚ü©‚ü©
    exact prop_of_iso _ (F.preimageIso e) hX
  ¬∑ intro hX
    exact ‚ü®‚ü®X, hX‚ü©, ‚ü®Iso.refl _‚ü©‚ü©

end

end Subcategory

end Triangulated

end CategoryTheory

/-! # Triangulated subcategories

New version. Should be merged, but... :-(
-/

assert_not_exists TwoSidedIdeal

namespace CategoryTheory

open Category Limits Preadditive ZeroObject Pretriangulated Triangulated

open Pretriangulated

variable {C : Type*} [Category C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace ObjectProperty

variable (P : ObjectProperty C)

/-- Given `P : ObjectProperty C` with `C` a pretriangulated category, this
is the property that whenever `X‚ÇÅ ‚ü∂ X‚ÇÇ ‚ü∂ X‚ÇÉ ‚ü∂ X‚ÇÅ‚ü¶1‚üß` is a distinguished triangle
such that `P X‚ÇÇ` and `P X‚ÇÉ` hold, then `P.isoClosure X‚ÇÅ` holds. -/
class IsTriangulatedClosed‚ÇÅ : Prop where
  ext‚ÇÅ' (T : Triangle C) (_ : T ‚àà distTriang C) : P T.obj‚ÇÇ ‚Üí P T.obj‚ÇÉ ‚Üí P.isoClosure T.obj‚ÇÅ

/-- Given `P : ObjectProperty C` with `C` a pretriangulated category, this
is the property that whenever `X‚ÇÅ ‚ü∂ X‚ÇÇ ‚ü∂ X‚ÇÉ ‚ü∂ X‚ÇÅ‚ü¶1‚üß` is a distinguished triangle
such that `P X‚ÇÅ` and `P X‚ÇÉ` hold, then `P.isoClosure X‚ÇÇ` holds. -/
class IsTriangulatedClosed‚ÇÇ : Prop where
  ext‚ÇÇ' (T : Triangle C) (_ : T ‚àà distTriang C) : P T.obj‚ÇÅ ‚Üí P T.obj‚ÇÉ ‚Üí P.isoClosure T.obj‚ÇÇ

/-- Given `P : ObjectProperty C` with `C` a pretriangulated category, this
is the property that whenever `X‚ÇÅ ‚ü∂ X‚ÇÇ ‚ü∂ X‚ÇÉ ‚ü∂ X‚ÇÅ‚ü¶1‚üß` is a distinguished triangle
such that `P X‚ÇÅ` and `P X‚ÇÇ` hold, then `P.isoClosure X‚ÇÉ` holds. -/
class IsTriangulatedClosed‚ÇÉ : Prop where
  ext‚ÇÉ' (T : Triangle C) (_ : T ‚àà distTriang C) : P T.obj‚ÇÅ ‚Üí P T.obj‚ÇÇ ‚Üí P.isoClosure T.obj‚ÇÉ

lemma ext_of_isTriangulatedClosed‚ÇÅ'
    [P.IsTriangulatedClosed‚ÇÅ] (T : Triangle C) (hT : T ‚àà distTriang C)
    (h‚ÇÇ : P T.obj‚ÇÇ) (h‚ÇÉ : P T.obj‚ÇÉ) : P.isoClosure T.obj‚ÇÅ :=
  IsTriangulatedClosed‚ÇÅ.ext‚ÇÅ' T hT h‚ÇÇ h‚ÇÉ

lemma ext_of_isTriangulatedClosed‚ÇÇ'
    [P.IsTriangulatedClosed‚ÇÇ] (T : Triangle C) (hT : T ‚àà distTriang C)
    (h‚ÇÅ : P T.obj‚ÇÅ) (h‚ÇÉ : P T.obj‚ÇÉ) : P.isoClosure T.obj‚ÇÇ :=
  IsTriangulatedClosed‚ÇÇ.ext‚ÇÇ' T hT h‚ÇÅ h‚ÇÉ

lemma ext_of_isTriangulatedClosed‚ÇÉ'
    [P.IsTriangulatedClosed‚ÇÉ] (T : Triangle C) (hT : T ‚àà distTriang C)
    (h‚ÇÅ : P T.obj‚ÇÅ) (h‚ÇÇ : P T.obj‚ÇÇ) : P.isoClosure T.obj‚ÇÉ :=
  IsTriangulatedClosed‚ÇÉ.ext‚ÇÉ' T hT h‚ÇÅ h‚ÇÇ

lemma ext_of_isTriangulatedClosed‚ÇÅ
    [P.IsTriangulatedClosed‚ÇÅ] [P.IsClosedUnderIsomorphisms]
    (T : Triangle C) (hT : T ‚àà distTriang C)
    (h‚ÇÇ : P T.obj‚ÇÇ) (h‚ÇÉ : P T.obj‚ÇÉ) : P T.obj‚ÇÅ := by
  simpa only [isoClosure_eq_self] using P.ext_of_isTriangulatedClosed‚ÇÅ' T hT h‚ÇÇ h‚ÇÉ

lemma ext_of_isTriangulatedClosed‚ÇÇ
    [P.IsTriangulatedClosed‚ÇÇ] [P.IsClosedUnderIsomorphisms]
    (T : Triangle C) (hT : T ‚àà distTriang C)
    (h‚ÇÅ : P T.obj‚ÇÅ) (h‚ÇÉ : P T.obj‚ÇÉ) : P T.obj‚ÇÇ := by
  simpa only [isoClosure_eq_self] using P.ext_of_isTriangulatedClosed‚ÇÇ' T hT h‚ÇÅ h‚ÇÉ

lemma ext_of_isTriangulatedClosed‚ÇÉ
    [P.IsTriangulatedClosed‚ÇÉ] [P.IsClosedUnderIsomorphisms]
    (T : Triangle C) (hT : T ‚àà distTriang C)
    (h‚ÇÅ : P T.obj‚ÇÅ) (h‚ÇÇ : P T.obj‚ÇÇ) : P T.obj‚ÇÉ := by
  simpa only [isoClosure_eq_self] using P.ext_of_isTriangulatedClosed‚ÇÉ' T hT h‚ÇÅ h‚ÇÇ

variable {P}

lemma IsTriangulatedClosed‚ÇÅ.mk' [P.IsClosedUnderIsomorphisms]
    (hP : ‚àÄ (T : Triangle C) (_ : T ‚àà distTriang C)
      (_ : P T.obj‚ÇÇ) (_ : P T.obj‚ÇÉ), P T.obj‚ÇÅ) : P.IsTriangulatedClosed‚ÇÅ where
  ext‚ÇÅ' := by simpa only [isoClosure_eq_self] using hP

lemma IsTriangulatedClosed‚ÇÇ.mk' [P.IsClosedUnderIsomorphisms]
    (hP : ‚àÄ (T : Triangle C) (_ : T ‚àà distTriang C)
      (_ : P T.obj‚ÇÅ) (_ : P T.obj‚ÇÉ), P T.obj‚ÇÇ) : P.IsTriangulatedClosed‚ÇÇ where
  ext‚ÇÇ' := by simpa only [isoClosure_eq_self] using hP

lemma IsTriangulatedClosed‚ÇÉ.mk' [P.IsClosedUnderIsomorphisms]
    (hP : ‚àÄ (T : Triangle C) (_ : T ‚àà distTriang C)
      (_ : P T.obj‚ÇÅ) (_ : P T.obj‚ÇÇ), P T.obj‚ÇÉ) : P.IsTriangulatedClosed‚ÇÉ where
  ext‚ÇÉ' := by simpa only [isoClosure_eq_self] using hP

variable (P)

instance [P.IsTriangulatedClosed‚ÇÇ] : P.isoClosure.IsTriangulatedClosed‚ÇÇ where
  ext‚ÇÇ' := by
    rintro T hT ‚ü®X‚ÇÅ, h‚ÇÅ, ‚ü®e‚ÇÅ‚ü©‚ü© ‚ü®X‚ÇÉ, h‚ÇÉ, ‚ü®e‚ÇÉ‚ü©‚ü©
    exact ObjectProperty.le_isoClosure _ _
      (P.ext_of_isTriangulatedClosed‚ÇÇ'
        (Triangle.mk (e‚ÇÅ.inv ‚â´ T.mor‚ÇÅ) (T.mor‚ÇÇ ‚â´ e‚ÇÉ.hom) (e‚ÇÉ.inv ‚â´ T.mor‚ÇÉ ‚â´ e‚ÇÅ.hom‚ü¶1‚üß'))
      (isomorphic_distinguished _ hT _
        (Triangle.isoMk _ _ e‚ÇÅ.symm (Iso.refl _) e‚ÇÉ.symm (by simp) (by simp) (by
          dsimp
          simp only [assoc, Iso.cancel_iso_inv_left, ‚Üê Functor.map_comp, e‚ÇÅ.hom_inv_id,
            Functor.map_id, comp_id]))) h‚ÇÅ h‚ÇÉ)

/-- The property that `P : ObjectProperty C` is a triangulated subcategory
(of a pretriangulated category `C`). -/
protected class IsTriangulated : Prop extends P.ContainsZero, P.IsStableUnderShift ‚Ñ§,
    P.IsTriangulatedClosed‚ÇÇ where

instance [P.IsTriangulated] : P.IsTriangulatedClosed‚ÇÅ where
  ext‚ÇÅ' _ hT h‚ÇÇ h‚ÇÉ :=
    P.ext_of_isTriangulatedClosed‚ÇÇ' _ (inv_rot_of_distTriang _ hT) (P.le_shift _ _ h‚ÇÉ) h‚ÇÇ

instance [P.IsTriangulated] : P.IsTriangulatedClosed‚ÇÉ where
  ext‚ÇÉ' _ hT h‚ÇÅ h‚ÇÇ :=
    P.ext_of_isTriangulatedClosed‚ÇÇ' _ (rot_of_distTriang _ hT) h‚ÇÇ (P.le_shift _ _ h‚ÇÅ)

instance [P.IsTriangulated] : P.isoClosure.IsTriangulated where

/-- Given `P : ObjectProperty C` with `C` a pretriangulated category, this is the class
of morphisms whose cone satisfies `S.P`. -/
def trW : MorphismProperty C :=
  fun X Y f => ‚àÉ (Z : C) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
    (_ : Triangle.mk f g h ‚àà distTriang C), P Z

lemma trW_iff {X Y : C} (f : X ‚ü∂ Y) :
    P.trW f ‚Üî ‚àÉ (Z : C) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
      (_ : Triangle.mk f g h ‚àà distTriang C), P Z := by rfl

lemma trW_iff' [P.IsStableUnderShift ‚Ñ§] {Y Z : C} (g : Y ‚ü∂ Z) :
    P.trW g ‚Üî ‚àÉ (X : C) (f : X ‚ü∂ Y) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
      (_ : Triangle.mk f g h ‚àà distTriang C), P X := by
  rw [P.trW_iff]
  constructor
  ¬∑ rintro ‚ü®Z, g, h, H, mem‚ü©
    exact ‚ü®_, _, _, inv_rot_of_distTriang _ H, P.le_shift (-1) _ mem‚ü©
  ¬∑ rintro ‚ü®Z, g, h, H, mem‚ü©
    exact ‚ü®_, _, _, rot_of_distTriang _ H, P.le_shift 1 _ mem‚ü©

lemma trW.mk {T : Triangle C} (hT : T ‚àà distTriang C) (h : P T.obj‚ÇÉ) : P.trW T.mor‚ÇÅ :=
  ‚ü®_, _, _, hT, h‚ü©

lemma trW.mk' [P.IsStableUnderShift ‚Ñ§] {T : Triangle C} (hT : T ‚àà distTriang C)
    (h : P T.obj‚ÇÅ) : P.trW T.mor‚ÇÇ := by
  rw [trW_iff']
  exact ‚ü®_, _, _, hT, h‚ü©

lemma trW_isoClosure : P.isoClosure.trW = P.trW := by
  ext X Y f
  constructor
  ¬∑ rintro ‚ü®Z, g, h, mem, ‚ü®Z', hZ', ‚ü®e‚ü©‚ü©‚ü©
    refine ‚ü®Z', g ‚â´ e.hom, e.inv ‚â´ h, isomorphic_distinguished _ mem _ ?_, hZ'‚ü©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) e.symm
  ¬∑ rintro ‚ü®Z, g, h, mem, hZ‚ü©
    exact ‚ü®Z, g, h, mem, ObjectProperty.le_isoClosure _ _ hZ‚ü©

instance : P.trW.RespectsIso where
  precomp {X' X Y} e (he : IsIso e) := by
    rintro f ‚ü®Z, g, h, mem, mem'‚ü©
    refine ‚ü®Z, g, h ‚â´ inv e‚ü¶(1 : ‚Ñ§)‚üß', isomorphic_distinguished _ mem _ ?_, mem'‚ü©
    refine Triangle.isoMk _ _ (asIso e) (Iso.refl _) (Iso.refl _) (by simp) (by simp) ?_
    dsimp
    simp only [Functor.map_inv, assoc, IsIso.inv_hom_id, comp_id, id_comp]
  postcomp {X Y Y'} e (he : IsIso e) := by
    rintro f ‚ü®Z, g, h, mem, mem'‚ü©
    refine ‚ü®Z, inv e ‚â´ g, h, isomorphic_distinguished _ mem _ ?_, mem'‚ü©
    exact Triangle.isoMk _ _ (Iso.refl _) (asIso e).symm (Iso.refl _)

instance [P.ContainsZero] : P.trW.ContainsIdentities := by
  rw [‚Üê trW_isoClosure]
  exact ‚ü®fun X => ‚ü®_, _, _, contractible_distinguished X, prop_zero _‚ü©‚ü©

lemma trW_of_isIso [P.ContainsZero] {X Y : C} (f : X ‚ü∂ Y) [IsIso f] : P.trW f := by
  refine (P.trW.arrow_mk_iso_iff ?_).1 (MorphismProperty.id_mem _ X)
  exact Arrow.isoMk (Iso.refl _) (asIso f)

lemma smul_mem_trW_iff {X Y : C} (f : X ‚ü∂ Y) (n : ‚Ñ§À£) :
    P.trW (n ‚Ä¢ f) ‚Üî P.trW f :=
  P.trW.arrow_mk_iso_iff (Arrow.isoMk (n ‚Ä¢ (Iso.refl _)) (Iso.refl _))

variable {P} in
lemma trW.shift [P.IsStableUnderShift ‚Ñ§]
    {X‚ÇÅ X‚ÇÇ : C} {f : X‚ÇÅ ‚ü∂ X‚ÇÇ} (hf : P.trW f) (n : ‚Ñ§) : P.trW (f‚ü¶n‚üß') := by
  rw [‚Üê smul_mem_trW_iff _ _ (n.negOnePow)]
  obtain ‚ü®X‚ÇÉ, g, h, hT, mem‚ü© := hf
  exact ‚ü®_, _, _, Pretriangulated.Triangle.shift_distinguished _ hT n, P.le_shift _ _ mem‚ü©

lemma trW.unshift [P.IsStableUnderShift ‚Ñ§]
    {X‚ÇÅ X‚ÇÇ : C} {f : X‚ÇÅ ‚ü∂ X‚ÇÇ} {n : ‚Ñ§} (hf : P.trW (f‚ü¶n‚üß')) : P.trW f :=
  (P.trW.arrow_mk_iso_iff
     (Arrow.isoOfNatIso (shiftEquiv C n).unitIso (Arrow.mk f))).2 (hf.shift (-n))

instance [P.IsStableUnderShift ‚Ñ§] : P.trW.IsCompatibleWithShift ‚Ñ§ where
  condition n := by
    ext K L f
    exact ‚ü®fun hf => hf.unshift, fun hf => hf.shift n‚ü©

instance [IsTriangulated C] [P.IsTriangulated] : P.trW.IsMultiplicative where
  comp_mem := by
    rw [‚Üê trW_isoClosure]
    rintro X‚ÇÅ X‚ÇÇ X‚ÇÉ u‚ÇÅ‚ÇÇ u‚ÇÇ‚ÇÉ ‚ü®Z‚ÇÅ‚ÇÇ, v‚ÇÅ‚ÇÇ, w‚ÇÅ‚ÇÇ, H‚ÇÅ‚ÇÇ, mem‚ÇÅ‚ÇÇ‚ü© ‚ü®Z‚ÇÇ‚ÇÉ, v‚ÇÇ‚ÇÉ, w‚ÇÇ‚ÇÉ, H‚ÇÇ‚ÇÉ, mem‚ÇÇ‚ÇÉ‚ü©
    obtain ‚ü®Z‚ÇÅ‚ÇÉ, v‚ÇÅ‚ÇÉ, w‚ÇÅ‚ÇÇ, H‚ÇÅ‚ÇÉ‚ü© := distinguished_cocone_triangle (u‚ÇÅ‚ÇÇ ‚â´ u‚ÇÇ‚ÇÉ)
    exact ‚ü®_, _, _, H‚ÇÅ‚ÇÉ, P.isoClosure.ext_of_isTriangulatedClosed‚ÇÇ
      _ (someOctahedron rfl H‚ÇÅ‚ÇÇ H‚ÇÇ‚ÇÉ H‚ÇÅ‚ÇÉ).mem mem‚ÇÅ‚ÇÇ mem‚ÇÇ‚ÇÉ‚ü©

lemma trW_iff_of_distinguished
    [P.IsClosedUnderIsomorphisms] (T : Triangle C) (hT : T ‚àà distTriang C) :
    P.trW T.mor‚ÇÅ ‚Üî P T.obj‚ÇÉ := by
  constructor
  ¬∑ rintro ‚ü®Z, g, h, hT', mem‚ü©
    obtain ‚ü®e, _‚ü© := exists_iso_of_arrow_iso _ _ hT' hT (Iso.refl _)
    exact P.prop_of_iso (Triangle.œÄ‚ÇÉ.mapIso e) mem
  ¬∑ intro h
    exact ‚ü®_, _, _, hT, h‚ü©

/-- Variant of `mem_W_iff_of_distinguished`. -/
lemma trW_iff_of_distinguished' [P.IsStableUnderShift ‚Ñ§]
    [P.IsClosedUnderIsomorphisms] (T : Triangle C) (hT : T ‚àà distTriang C) :
    P.trW T.mor‚ÇÇ ‚Üî P T.obj‚ÇÅ := by
  simpa [P.prop_shift_iff_of_isStableUnderShift]
    using P.trW_iff_of_distinguished _ (rot_of_distTriang _ hT)

instance [IsTriangulated C] [P.IsTriangulated] : P.trW.HasLeftCalculusOfFractions where
  exists_leftFraction X Y œÜ := by
    obtain ‚ü®Z, f, g, H, mem‚ü© := œÜ.hs
    obtain ‚ü®Y', s', f', mem'‚ü© := distinguished_cocone_triangle‚ÇÇ (g ‚â´ œÜ.f‚ü¶1‚üß')
    obtain ‚ü®b, ‚ü®hb‚ÇÅ, _‚ü©‚ü© :=
      complete_distinguished_triangle_morphism‚ÇÇ _ _ H mem' œÜ.f (ùüô Z) (by simp)
    exact ‚ü®MorphismProperty.LeftFraction.mk b s' ‚ü®_, _, _, mem', mem‚ü©, hb‚ÇÅ.symm‚ü©
  ext := by
    rintro X' X Y f‚ÇÅ f‚ÇÇ s ‚ü®Z, g, h, H, mem‚ü© hf‚ÇÅ
    have hf‚ÇÇ : s ‚â´ (f‚ÇÅ - f‚ÇÇ) = 0 := by rw [comp_sub, hf‚ÇÅ, sub_self]
    obtain ‚ü®q, hq‚ü© := Triangle.yoneda_exact‚ÇÇ _ H _ hf‚ÇÇ
    obtain ‚ü®Y', r, t, mem'‚ü© := distinguished_cocone_triangle q
    refine ‚ü®Y', r, ?_, ?_‚ü©
    ¬∑ exact ‚ü®_, _, _, rot_of_distTriang _ mem', P.le_shift _ _ mem‚ü©
    ¬∑ have eq := comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _ mem'
      dsimp at eq
      rw [‚Üê sub_eq_zero, ‚Üê sub_comp, hq, assoc, eq, comp_zero]

instance [IsTriangulated C] [P.IsTriangulated] : P.trW.HasRightCalculusOfFractions where
  exists_rightFraction X Y œÜ := by
    obtain ‚ü®Z, f, g, H, mem‚ü© := œÜ.hs
    obtain ‚ü®X', f', h', mem'‚ü© := distinguished_cocone_triangle‚ÇÅ (œÜ.f ‚â´ f)
    obtain ‚ü®a, ‚ü®ha‚ÇÅ, _‚ü©‚ü© := complete_distinguished_triangle_morphism‚ÇÅ _ _
      mem' H œÜ.f (ùüô Z) (by simp)
    exact ‚ü®MorphismProperty.RightFraction.mk f' ‚ü®_, _, _, mem', mem‚ü© a, ha‚ÇÅ‚ü©
  ext Y Z Z' f‚ÇÅ f‚ÇÇ s hs hf‚ÇÅ := by
    rw [P.trW_iff'] at hs
    obtain ‚ü®Z, g, h, H, mem‚ü© := hs
    have hf‚ÇÇ : (f‚ÇÅ - f‚ÇÇ) ‚â´ s = 0 := by rw [sub_comp, hf‚ÇÅ, sub_self]
    obtain ‚ü®q, hq‚ü© := Triangle.coyoneda_exact‚ÇÇ _ H _ hf‚ÇÇ
    obtain ‚ü®Y', r, t, mem'‚ü© := distinguished_cocone_triangle‚ÇÅ q
    refine ‚ü®Y', r, ?_, ?_‚ü©
    ¬∑ exact ‚ü®_, _, _, mem', mem‚ü©
    ¬∑ have eq := comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _ mem'
      dsimp at eq
      rw [‚Üê sub_eq_zero, ‚Üê comp_sub, hq, reassoc_of% eq, zero_comp]

instance [IsTriangulated C] [P.IsTriangulated] : P.trW.IsCompatibleWithTriangulation := ‚ü®by
  rintro T‚ÇÅ T‚ÇÉ mem‚ÇÅ mem‚ÇÉ a b ‚ü®Z‚ÇÖ, g‚ÇÖ, h‚ÇÖ, mem‚ÇÖ, mem‚ÇÖ'‚ü© ‚ü®Z‚ÇÑ, g‚ÇÑ, h‚ÇÑ, mem‚ÇÑ, mem‚ÇÑ'‚ü© comm
  obtain ‚ü®Z‚ÇÇ, g‚ÇÇ, h‚ÇÇ, mem‚ÇÇ‚ü© := distinguished_cocone_triangle (T‚ÇÅ.mor‚ÇÅ ‚â´ b)
  have H := someOctahedron rfl mem‚ÇÅ mem‚ÇÑ mem‚ÇÇ
  have H' := someOctahedron comm.symm mem‚ÇÖ mem‚ÇÉ mem‚ÇÇ
  let œÜ : T‚ÇÅ ‚ü∂ T‚ÇÉ := H.triangleMorphism‚ÇÅ ‚â´ H'.triangleMorphism‚ÇÇ
  exact ‚ü®œÜ.hom‚ÇÉ, P.trW.comp_mem _ _ (trW.mk P H.mem mem‚ÇÑ') (trW.mk' P H'.mem mem‚ÇÖ'),
    by simpa [œÜ] using œÜ.comm‚ÇÇ, by simpa [œÜ] using œÜ.comm‚ÇÉ‚ü©‚ü©

lemma binary_product_stable_of_isTriangulated [P.IsTriangulated] [P.IsClosedUnderIsomorphisms]
    (X‚ÇÅ X‚ÇÇ : C) (hX‚ÇÅ : P X‚ÇÅ) (hX‚ÇÇ : P X‚ÇÇ) :
    P (X‚ÇÅ ‚®Ø X‚ÇÇ)  :=
  P.ext_of_isTriangulatedClosed‚ÇÇ _ (binaryProductTriangle_distinguished X‚ÇÅ X‚ÇÇ) hX‚ÇÅ hX‚ÇÇ

lemma pi_finite_stable [P.IsTriangulated] [P.IsClosedUnderIsomorphisms]
    {J : Type} [Finite J] (X : J ‚Üí C) (hX : ‚àÄ j, P (X j)) :
    P (‚àè·∂ú X) := by
  revert hX X
  let Q : Type ‚Üí Prop := fun J =>
    ‚àÄ [hJ : Finite J] (X : J ‚Üí C) (_ : ‚àÄ j, P (X j)), P (‚àè·∂ú X)
  suffices Q J by convert this
  apply @Finite.induction_empty_option
  ¬∑ intro J‚ÇÅ J‚ÇÇ e hJ‚ÇÅ _ X hX
    have : Finite J‚ÇÅ := Finite.of_equiv _ e.symm
    exact prop_of_iso _ (productIsoOfEquiv X e) (hJ‚ÇÅ (fun j‚ÇÅ => X (e j‚ÇÅ)) (fun j‚ÇÅ => hX _))
  ¬∑ intro _ X _
    refine prop_of_iso _ (IsZero.isoZero ?_).symm P.prop_zero
    rw [IsZero.iff_id_eq_zero]
    ext ‚ü®‚ü©
  ¬∑ intro J _ hJ _ X hX
    exact prop_of_iso _ (productOptionIso  X).symm
      (P.binary_product_stable_of_isTriangulated _ _
        (hJ (fun j => X (some j)) (fun j => hX _)) (hX none))

instance [P.IsTriangulated] : P.trW.IsStableUnderFiniteProducts := by
  rw [‚Üê trW_isoClosure]
  exact ‚ü®fun J _ => by
    refine MorphismProperty.IsStableUnderProductsOfShape.mk _ _ ?_
    intro _ _ X‚ÇÅ X‚ÇÇ f hf
    exact trW.mk _ (productTriangle_distinguished _
      (fun j => (hf j).choose_spec.choose_spec.choose_spec.choose))
      (pi_finite_stable _ _ (fun j => (hf j).choose_spec.choose_spec.choose_spec.choose_spec))‚ü©

lemma closedUnderLimitsOfShape_discrete_of_isTriangulated
    [P.IsTriangulated] [P.IsClosedUnderIsomorphisms] (J : Type) [Finite J] :
    ClosedUnderLimitsOfShape (Discrete J) P := by
  intro F c hc hF
  let G (j : J) : C := F.obj ‚ü®j‚ü©
  have e : Discrete.functor G ‚âÖ F := Discrete.natIso (fun _ ‚Ü¶ Iso.refl _)
  have := IsLimit.conePointUniqueUpToIso (limit.isLimit _)
    ((IsLimit.postcomposeInvEquiv e c).2 hc)
  exact P.prop_of_iso this (P.pi_finite_stable G (fun j ‚Ü¶ hF _))

section

instance (P' : ObjectProperty C) [P.IsTriangulatedClosed‚ÇÇ] [P.IsClosedUnderIsomorphisms]
    [P'.IsTriangulatedClosed‚ÇÇ] :
    (P ‚äì P').IsTriangulatedClosed‚ÇÇ where
  ext‚ÇÇ' T hT h‚ÇÅ h‚ÇÉ := by
    obtain ‚ü®X‚ÇÇ, h‚ÇÇ, ‚ü®e‚ü©‚ü© := P'.ext_of_isTriangulatedClosed‚ÇÇ' T hT h‚ÇÅ.2 h‚ÇÉ.2
    exact ‚ü®X‚ÇÇ, ‚ü®P.prop_of_iso e (P.ext_of_isTriangulatedClosed‚ÇÇ T hT h‚ÇÅ.1 h‚ÇÉ.1), h‚ÇÇ‚ü©, ‚ü®e‚ü©‚ü©

instance (P' : ObjectProperty C) [P.IsTriangulated] [P.IsClosedUnderIsomorphisms]
    [P'.IsTriangulated] :
    (P ‚äì P').IsTriangulated where

end

section

variable [IsTriangulated C] [P.IsTriangulated]

noncomputable example : Pretriangulated (P.trW.Localization) := inferInstance
example : IsTriangulated (P.trW.Localization) := inferInstance
example : P.trW.Q.IsTriangulated := inferInstance

end

example : Preadditive P.FullSubcategory := inferInstance
example : P.Œπ.Additive := inferInstance

section

variable [P.IsTriangulated]

noncomputable instance hasShift :
    HasShift P.FullSubcategory ‚Ñ§ :=
  P.fullyFaithfulŒπ.hasShift (fun n ‚Ü¶ ObjectProperty.lift _ (P.Œπ ‚ãô shiftFunctor C n)
    (fun X ‚Ü¶ P.le_shift n _ X.2)) (fun _ => P.liftCompŒπIso _ _)

instance commShiftŒπ : P.Œπ.CommShift ‚Ñ§ :=
  Functor.CommShift.of_hasShiftOfFullyFaithful _ _ _

-- these definitions are made irreducible to prevent (at least temporarily) any abuse of defeq
attribute [irreducible] hasShift commShiftŒπ

instance (n : ‚Ñ§) : (shiftFunctor P.FullSubcategory n).Additive := by
  have := Functor.additive_of_iso (P.Œπ.commShiftIso n).symm
  apply Functor.additive_of_comp_faithful _ P.Œπ

instance : HasZeroObject P.FullSubcategory where
  zero := by
    obtain ‚ü®Z, hZ, mem‚ü© := P.exists_prop_of_containsZero
    refine ‚ü®‚ü®Z, mem‚ü©, ?_‚ü©
    rw [IsZero.iff_id_eq_zero]
    apply hZ.eq_of_src

attribute [local simp] ObjectProperty.fullyFaithfulŒπ fullyFaithfulInducedFunctor

noncomputable instance : Pretriangulated P.FullSubcategory where
  distinguishedTriangles := fun T => P.Œπ.mapTriangle.obj T ‚àà distTriang C
  isomorphic_distinguished := fun T‚ÇÅ hT‚ÇÅ T‚ÇÇ e =>
    isomorphic_distinguished _ hT‚ÇÅ _ (P.Œπ.mapTriangle.mapIso e)
  contractible_distinguished X := by
    refine isomorphic_distinguished _ (contractible_distinguished (P.Œπ.obj X)) _ ?_
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) P.Œπ.mapZeroObject
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  distinguished_cocone_triangle {X Y} f := by
    obtain ‚ü®Z', g', h', mem‚ü© := distinguished_cocone_triangle (P.Œπ.map f)
    obtain ‚ü®Z'', hZ'', ‚ü®e‚ü©‚ü© := P.ext_of_isTriangulatedClosed‚ÇÉ' _ mem X.2 Y.2
    let Z : P.FullSubcategory := ‚ü®Z'', hZ''‚ü©
    refine ‚ü®Z, P.fullyFaithfulŒπ.preimage (g' ‚â´ e.hom),
      P.fullyFaithfulŒπ.preimage (e.inv ‚â´ h' ‚â´ (P.Œπ.commShiftIso (1 : ‚Ñ§)).inv.app X),
      isomorphic_distinguished _ mem _ ?_‚ü©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) e.symm
      (by aesop_cat) (by simp) (by simp)
  rotate_distinguished_triangle T :=
    (rotate_distinguished_triangle (P.Œπ.mapTriangle.obj T)).trans
      (distinguished_iff_of_iso (P.Œπ.mapTriangleRotateIso.app T))
  complete_distinguished_triangle_morphism T‚ÇÅ T‚ÇÇ hT‚ÇÅ hT‚ÇÇ a b comm := by
    obtain ‚ü®c, ‚ü®hc‚ÇÅ, hc‚ÇÇ‚ü©‚ü© := complete_distinguished_triangle_morphism (P.Œπ.mapTriangle.obj T‚ÇÅ)
      (P.Œπ.mapTriangle.obj T‚ÇÇ) hT‚ÇÅ hT‚ÇÇ (P.Œπ.map a) (P.Œπ.map b)
      (by simpa using P.Œπ.congr_map comm)
    have ‚ü®c', hc'‚ü© : ‚àÉ (c' : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ), c = P.Œπ.map c' :=
      ‚ü®P.fullyFaithfulŒπ.preimage c, by simp‚ü©
    dsimp at hc‚ÇÅ hc‚ÇÇ
    rw [hc'] at hc‚ÇÅ
    rw [hc', assoc] at hc‚ÇÇ
    dsimp at hc‚ÇÇ
    erw [‚Üê Functor.commShiftIso_hom_naturality] at hc‚ÇÇ
    refine ‚ü®c', ‚ü®P.Œπ.map_injective ?_, P.Œπ.map_injective ?_‚ü©‚ü©
    ¬∑ simpa using hc‚ÇÅ
    ¬∑ rw [‚Üê cancel_mono ((Functor.commShiftIso P.Œπ (1 : ‚Ñ§)).hom.app T‚ÇÇ.obj‚ÇÅ),
        P.Œπ.map_comp, P.Œπ.map_comp, assoc, assoc]
      erw [hc‚ÇÇ]
      rfl

instance : P.Œπ.IsTriangulated := ‚ü®fun _ hT => hT‚ü©

instance [IsTriangulated C] : IsTriangulated P.FullSubcategory :=
  IsTriangulated.of_fully_faithful_triangulated_functor P.Œπ

section

variable {D : Type*} [Category D] [HasZeroObject D] [Preadditive D]
    [HasShift D ‚Ñ§] [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
    (F : C ‚•§ D) [F.CommShift ‚Ñ§] [F.IsTriangulated] [F.Full]

instance : (F.essImage).IsTriangulated where
  isStableUnderShiftBy n :=
    { le_shift := by
        rintro Y ‚ü®X, ‚ü®e‚ü©‚ü©
        exact ‚ü®X‚ü¶n‚üß, ‚ü®(F.commShiftIso n).app _ ‚â™‚â´ (shiftFunctor D n).mapIso e‚ü©‚ü© }
  exists_zero := ‚ü®0, isZero_zero D, ‚ü®0, ‚ü®F.mapZeroObject‚ü©‚ü©‚ü©
  toIsTriangulatedClosed‚ÇÇ := .mk' (by
    rintro T hT ‚ü®X‚ÇÅ, ‚ü®e‚ÇÅ‚ü©‚ü© ‚ü®X‚ÇÉ, ‚ü®e‚ÇÉ‚ü©‚ü©
    have ‚ü®h, hh‚ü© := F.map_surjective (e‚ÇÉ.hom ‚â´ T.mor‚ÇÉ ‚â´ e‚ÇÅ.inv‚ü¶1‚üß' ‚â´
      (F.commShiftIso (1 : ‚Ñ§)).inv.app X‚ÇÅ)
    obtain ‚ü®X‚ÇÇ, f, g, H‚ü© := distinguished_cocone_triangle‚ÇÇ h
    exact ‚ü®X‚ÇÇ, ‚ü®Triangle.œÄ‚ÇÇ.mapIso
      (isoTriangleOfIso‚ÇÅ‚ÇÉ _ _ (F.map_distinguished _ H) hT e‚ÇÅ e‚ÇÉ (by
        dsimp
        simp only [hh, assoc, Iso.inv_hom_id_app, Functor.comp_obj,
          comp_id, Iso.cancel_iso_hom_left, ‚Üê Functor.map_comp,
          Iso.inv_hom_id, Functor.map_id]))‚ü©‚ü©)



end

section

variable {D : Type*} [Category D] (F : D ‚•§ C) (hF : ‚àÄ (X : D), P (F.obj X))

-- some of these are general API, not specific to triangulated subcategories

instance [F.Faithful] : (P.lift F hF).Faithful :=
  Functor.Faithful.of_comp_iso (P.liftCompŒπIso F hF)

instance [F.Full] : (P.lift F hF).Full :=
  Functor.Full.of_comp_faithful_iso (P.liftCompŒπIso F hF)

-- should be generalized
instance [Preadditive D] [F.Additive] : (P.lift F hF).Additive where
  map_add {X Y f g} := by
    apply P.Œπ.map_injective
    apply F.map_add

noncomputable instance [HasShift D ‚Ñ§] [F.CommShift ‚Ñ§] : (P.lift F hF).CommShift ‚Ñ§ :=
  Functor.CommShift.ofComp (P.liftCompŒπIso F hF) ‚Ñ§

noncomputable instance [HasShift D ‚Ñ§] [F.CommShift ‚Ñ§] :
  NatTrans.CommShift (P.liftCompŒπIso F hF).hom ‚Ñ§ :=
    Functor.CommShift.ofComp_compatibility _ _

instance isTriangulated_lift [HasShift D ‚Ñ§] [Preadditive D] [F.CommShift ‚Ñ§] [HasZeroObject D]
    [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D] [F.IsTriangulated]:
    (P.lift F hF).IsTriangulated := by
  rw [Functor.isTriangulated_iff_comp_right (P.liftCompŒπIso F hF)]
  infer_instance

end

section

variable {D : Type*} [Category D] [Preadditive D] [HasZeroObject D] [HasShift D ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
  (F : D ‚•§ C) [F.CommShift ‚Ñ§] [F.IsTriangulated]
  [P.IsClosedUnderIsomorphisms]

instance : (P.inverseImage F).IsTriangulated where
  isStableUnderShiftBy n :=
    { le_shift _ hY := P.prop_of_iso ((F.commShiftIso n).symm.app _) (P.le_shift n _ hY) }
  toIsTriangulatedClosed‚ÇÇ := .mk' (fun T hT h‚ÇÅ h‚ÇÉ ‚Ü¶
    P.ext_of_isTriangulatedClosed‚ÇÇ _ (F.map_distinguished T hT) h‚ÇÅ h‚ÇÉ)

omit [P.IsTriangulated] in
lemma inverseImage_trW_iff {X Y : D} (s : X ‚ü∂ Y) :
    (P.inverseImage F).trW s ‚Üî P.trW (F.map s) := by
  obtain ‚ü®Z, g, h, hT‚ü© := distinguished_cocone_triangle s
  have eq‚ÇÅ := (P.inverseImage F).trW_iff_of_distinguished _ hT
  have eq‚ÇÇ := P.trW_iff_of_distinguished _ (F.map_distinguished _ hT)
  dsimp at eq‚ÇÅ eq‚ÇÇ
  rw [eq‚ÇÅ, prop_inverseImage_iff, eq‚ÇÇ]

omit [P.IsTriangulated] in
lemma inverseImage_W_isInverted {E : Type*} [Category E]
    (L : C ‚•§ E) [L.IsLocalization P.trW] :
    (P.inverseImage F).trW.IsInvertedBy (F ‚ãô L) :=
  fun X Y f hf => Localization.inverts L P.trW (F.map f)
    (by simpa only [inverseImage_trW_iff] using hf)

end

section

variable {D : Type*} [Category D] [Preadditive D] [HasZeroObject D] [HasShift D ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
  {F G : C ‚•§ D} [F.CommShift ‚Ñ§] [G.CommShift ‚Ñ§] [F.IsTriangulated]
  [G.IsTriangulated] (œÑ : F ‚ü∂ G) [NatTrans.CommShift œÑ ‚Ñ§]

def ofNatTrans : ObjectProperty C := fun X ‚Ü¶ IsIso (œÑ.app X)

instance : (ofNatTrans œÑ).IsClosedUnderIsomorphisms where
  of_iso e h := by
    dsimp [ofNatTrans] at h ‚ä¢
    rwa [‚Üê NatTrans.isIso_app_iff_of_iso œÑ e]

instance : (ofNatTrans œÑ).IsTriangulated where
  exists_zero := ‚ü®0, isZero_zero C,
    ‚ü®0, (F.map_isZero (isZero_zero C)).eq_of_src _ _,
      (G.map_isZero (isZero_zero C)).eq_of_src _ _‚ü©‚ü©
  isStableUnderShiftBy n :=
    { le_shift X (hX : IsIso _) := by
        simp [prop_shift_iff, ofNatTrans]
        rw [NatTrans.app_shift œÑ]
        infer_instance }
  toIsTriangulatedClosed‚ÇÇ := .mk' (fun T hT _ _ ‚Ü¶ by
    exact Pretriangulated.isIso‚ÇÇ_of_isIso‚ÇÅ‚ÇÉ
        ((Pretriangulated.Triangle.homMk _ _ (œÑ.app _) (œÑ.app _) (œÑ.app _)
          (by simp) (by simp) (by simp [NatTrans.shift_app_comm])))
        (F.map_distinguished _ hT) (G.map_distinguished _ hT) (by assumption) (by assumption))

end

section

variable {D : Type*} [Category D] [HasZeroObject D] [Preadditive D]
    [HasShift D ‚Ñ§] [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]
    (F : C ‚•§ D) [F.CommShift ‚Ñ§] [F.IsTriangulated] [F.Full] [F.Faithful]

instance : (P.map F).IsTriangulated := by
  convert inferInstanceAs (P.Œπ ‚ãô F).essImage.IsTriangulated
  ext Y
  constructor
  ¬∑ rintro ‚ü®X, hX, ‚ü®e‚ü©‚ü©
    exact ‚ü®‚ü®X, hX‚ü©, ‚ü®e‚ü©‚ü©
  ¬∑ rintro ‚ü®X, ‚ü®e‚ü©‚ü©
    exact ‚ü®X.1, X.2, ‚ü®e‚ü©‚ü©


end

end

end ObjectProperty

end CategoryTheory
